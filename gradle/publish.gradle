apply plugin: 'maven-publish'

def getBranch() {
    def process = 'git branch --show-current'.execute()
    process.waitFor()
    return process.text.trim()
}

def getHash() {
    def process = 'git rev-parse HEAD'.execute()
    process.waitFor()
    return process.text.trim()
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes (
                'Build-Jdk': "${System.properties['java.vendor']} ${System.properties['java.vm.version']}",
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Git-Branch': getBranch(),
                'Git-Hash': getHash()
                )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = packageName
            from components.java
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "${artifactory_contextUrl}/libs-release-local"
            def snapshotsRepoUrl = "${artifactory_contextUrl}/libs-snapshot-local"
            if (version.endsWith('SNAPSHOT')) {
                url = snapshotsRepoUrl
            } else {
                url = releasesRepoUrl
            }
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}