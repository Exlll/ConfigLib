subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    apply from: "$rootDir/gradle/jacoco.gradle"
    apply from: "$rootDir/gradle/publish.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()

        maven {
            name = 'Spigot'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }

        maven { url = 'https://artifactory.silthus.net/artifactory/libs-release' }
        maven { url = 'https://artifactory.silthus.net/artifactory/libs-snapshot' }

        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    }

    dependencies {
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.0.3'
        testCompile group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.0.3'
        testCompile group: 'org.junit.platform', name: 'junit-platform-suite-api', version: '1.0.3'
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'com.google.jimfs', name: 'jimfs', version: '1.1'
    }


}

project(':configlib-core') {
    dependencies { compile group: 'org.yaml', name: 'snakeyaml', version: '1.20' }
}

project(':configlib-bukkit') {
    dependencies {
        compile project(':configlib-core')
        compile 'org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT'
    }
    jar { from { project(':configlib-core').sourceSets.main.output } }
}

project(':configlib-bungee') {
    dependencies {
        compile project(':configlib-core')
        compile group: 'net.md-5', name: 'bungeecord-api', version: '1.15-SNAPSHOT'
    }
    jar { from { project(':configlib-core').sourceSets.main.output } }
}